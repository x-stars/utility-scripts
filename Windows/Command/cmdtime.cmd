@ REM Measure command execution time.
@ SETLOCAL ENABLEEXTENSIONS
@ SET ERRORLEVEL=& SET TIME=

@ SET TIME0=%TIME%
@ CALL %*
@ SET EXITCODE=%ERRORLEVEL%
@ SET TIME1=%TIME%
@ ECHO>CON:=

@ SET HOUR0=%TIME0:~0,2%
@ IF %HOUR0% LEQ 9 @ SET HOUR0=%HOUR0:~1,1%
@ SET  MIN0=%TIME0:~3,2%
@ IF  %MIN0% LEQ 9 @ SET  MIN0=%MIN0:~1,1%
@ SET  SEC0=%TIME0:~6,2%
@ IF  %SEC0% LEQ 9 @ SET  SEC0=%SEC0:~1,1%
@ SET CSEC0=%TIME0:~9,2%
@ IF %CSEC0% LEQ 9 @ SET CSEC0=%CSEC0:~1,1%
@ SET /A TICK0 = %HOUR0% * 360000 + ^
    %MIN0% * 6000 + %SEC0% * 100 + %CSEC0%

@ SET HOUR1=%TIME1:~0,2%
@ IF %HOUR1% LEQ 9 @ SET HOUR1=%HOUR1:~1,1%
@ SET  MIN1=%TIME1:~3,2%
@ IF  %MIN1% LEQ 9 @ SET  MIN1=%MIN1:~1,1%
@ SET  SEC1=%TIME1:~6,2%
@ IF  %SEC1% LEQ 9 @ SET  SEC1=%SEC1:~1,1%
@ SET CSEC1=%TIME1:~9,2%
@ IF %CSEC1% LEQ 9 @ SET CSEC1=%CSEC1:~1,1%
@ SET /A TICK1 = %HOUR1% * 360000 + ^
    %MIN1% * 6000 + %SEC1% * 100 + %CSEC1%

@ SET /A TICKS = %TICK1% - %TICK0%
@ IF %TICKS% LSS 0 @ (
    @ SET /A TICKS = %TICKS% + 24 * 360000
)
@ SET /A HOURS = %TICKS%  / 360000
@ IF %HOURS% LEQ 9 @ SET HOURS= %HOURS%
@ SET /A  MINS = %TICKS% %% 360000 / 6000
@ IF  %MINS% LEQ 9 @ SET  MINS=0%MINS%
@ SET /A  SECS = %TICKS% %%   6000 /  100
@ IF  %SECS% LEQ 9 @ SET  SECS=0%SECS%
@ SET /A CSECS = %TICKS% %%    100
@ IF %CSECS% LEQ 9 @ SET CSECS=0%CSECS%

@ ECHO>CON: %HOURS%:%MINS%:%SECS%.%CSECS%
@ EXIT /B %EXITCODE%
